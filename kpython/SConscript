# Hey, Emacs! This is a -*- Python -*- file!
#
# Copyright (C) 2006-2012 Opersys inc.
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import os
Import('env')

python_FILES = [
    '__init__.py',
    'kbase.py',
    'config.py',
    'kdaemonize.py',
    'kdebug.py',
    'kexecpg.py',
    'kexternalrunner.py',
    'kfile.py',
    'kfilter.py',
    'kflags.py',
    'kgetstrings.py',
    'kifconfig.py',
    'kiniupdater.py',
    'klog.py',
    'kodict.py',
    'koptions.py',
    'kout.py',
    'kpatch.py',
    'kpg.py',
    'kprocmonitor.py',
    'kprompt.py',
    'kproperty.py',
    'kreadline.py',
    'krun.py',
    'ksort.py',
    'ksudo.py',
    'kvalues.py',
    'kxmlrpc.py']

python_PROGS = [
    'kexecpg.py',
    'kiniupdater.py',
    'kprocmonitor.py',
    'kexternalrunner.py']

install = []
builds = []

pydir = env.Dir(env['PYTHONDIR'])
bindir = env.Dir(env['BINDIR'])

env.Textfile('config.py', ["CONF_DIR = '$CONFDIR'",
                           "PREFIX = '$PREFIX'",
                           "BINDIR = '$BINDIR'",
                           "PYTHON_DIR = '$PYTHONDIR'"])

for pf in python_FILES:
    installed_py = env.Install(dir = pydir, target = pf)
    install.append(installed_py)

for pp in python_PROGS:
    shName = os.path.splitext(os.path.basename(pp))[0]
    # Predefine LINESEPARATOR or Textfile will choke.
    env['LINESEPARATOR'] = os.linesep
    psh = env.Textfile(shName, ["#!/bin/sh",
                                "export PYTHONPATH=$PYTHONDIR",
                                "$PYTHONDIR/" + str(pp) + ' $@'])
    install.append(env.Install(dir = env['PYTHONDIR'], target = pp))
    shInst = env.Install(dir = env['BINDIR'], target = psh)
    env.AddPostAction(shInst, Chmod("$TARGET", 0755))
    install.append(shInst)
